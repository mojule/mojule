/*
  nodes are created with tags
*/
root>
  foo: properties which are children of a tag become properties of the node model
  types>
    // inline string syntax
    foo: unquoted string with spaces is parsed as a single string
    // quoted strings are the same as JSON strings
    bar: "strings can also be quoted and contain \"escaped quotes\""
    // numbers are unquoted
    baz: 15
    // if a string looks like a number, quote it
    qux: "1.16"
    // constants - if a string looks like a constant, quote it
    isFoo: true
    isBar: false
    optional: null
    /*
      Inline arrays are space separated. Strings containing spaces must be
      quoted
    */
    foos[] string1 string2 string3 42 true false null "Hello, World!"
    // object syntax
    barbaz{}
      foo: true
      bar: 3.1415
  complex>
    // multiline array - blanks are ignored - use "" for empty strings in arrays
    bars[]
      foo
      ""
      bar

      baz
    // large arrays can be space separated across multiple lines
    large[]
      isNode isValue createRawNode getChildren getValue setValue remove add get
      getRoot map clone ancestors childAt closest descendants firstChild
      getParent lastChild nextSibling previousSibling siblings walk walkUp
      accepts atPath contains find findAll getMeta getPath hasChild hasChildren
      index isEmpty meta nodeType setMeta slug append empty insertAfter insertAt
      insertBefore prepend prune removeAt true replaceChild false unwrap null
      value wrap foo bar baz qux "Value with spaces" helloWorld 42
    nested[]
      isNode isValue createRawNode getChildren getValue setValue remove add get
      /*
        nested arrays have the same syntax as inline and multiline arrays, just
        without an indentifier. They must be on a new line
      */
      [] "this is an inline nested array" 1 2 3
      // nested objects
      {}
        foo: 42
        bar: "42"
      // deep nesting
      []
        getRoot map clone ancestors childAt closest descendants firstChild
        [] foo bar baz
        {}
          foo: 42
          bar: "42"
  heredocs>
    description: a heredoc syntax is supported for multiline strings
    /*
      Leading indentation up to the level of nesting is removed:

He thrusts his fists
   against the
  posts
and
        still
  insists he
            sees the ghosts
    */
    rhyme$
      He thrusts his fists
         against the
        posts

      and
              still
        insists he
                  sees the ghosts